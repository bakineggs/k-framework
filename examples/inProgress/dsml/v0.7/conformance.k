require ocl-semantics
kmod CONFORMANCE is including OCL-SEMANTICS


kvar INSTANCES REST B RESTBAG RESTINSTANCES : Bag
kvar ATTRCLS ATTRINST Map Map' Map'' : Map 
kvar C T nm INSNAME attr x : Id 
kvar OCL : List
kvar E : Exp
kvar v : Val

syntax K ::= checkConformance 


rule

<k> checkConformance => checkStructure(INSTANCES)~>checkOCL(OCL)<_/k> 
<oclConstraints> OCL => . </oclConstraints>
<models> <model> <modelName> nm </modelName> INSTANCES </model> </models>




syntax K ::= checkStructure ( Bag )


rule <k> checkStructure(.Bag) => (.) <_/k> 


syntax K ::= pause

rule <k> checkStructure(
                    (<instance_>            
                      <attributes> ATTRINST </attributes>
                      <ofClass> C </ofClass> 
                    <_/instance>
                       ) REST)
         => 
         consistentAttributes(ATTRINST, attributeDecl(C))~>checkStructure(REST)
     <_/k>


syntax K ::= consistentAttributes(  Map , Map )

rule <k> consistentAttributes( .Map, _ ) => . <_/k> 


// for basic types : just check that the attribute is declared in metamodel
rule  <k> consistentAttributes( (attr |->  typedElt(v,T)) Map',  
                            (attr |-> T ) Map'')
      =>
      consistentAttributes(Map',Map'') <_/k>
      if isBasic(T)


//for enumerations : check that the attribute is declared with same enum type; check value
//assumption (made in metamodels) : no class and enum have same name
rule  <k> consistentAttributes( (attr |->  typedElt(val(BagItem(x)),T)) Map',  
                            (attr |-> T ) Map'')
      =>
      consistentAttributes(Map',Map'') <_/k>
     if isEnum(T) andBool (x in values(T))

// isEnum, values : generated by Rascal in META-MODEL


// for classes or collections : check that instances are also 
// in the model and are of the correct (declared) type
rule  <k> consistentAttributes((attr |->  typedElt(v,T)) Map',  //T here may be a subtype of
                            (attr |-> T ) Map'')   // T here
      => 
      allPresentAndWellTyped(v)~>consistentAttributes(Map',Map'')
      <_/k>
      if notBool (isBasic(T) orBool isEnum(T))



syntax Bool ::= isBasic ( Id )
rule isBasic(int) => true [structural]
rule isBasic(bool) => true [structural]
rule isBasic(string) => true [structural]
rule isBasic(C) => false
if (( C =/=Bool int) andBool (C =/=Bool bool) andBool  (C =/=Bool string)) [structural]




syntax K ::= allPresentAndWellTyped( Val )
// all elements in a collection must exist  among instances 
rule <k> allPresentAndWellTyped(val(.Bag)) => . <_/k> 

rule <k> allPresentAndWellTyped(val(BagItem(INSNAME : T) RESTBAG)) =>  
         allPresentAndWellTyped(val(RESTBAG))
     <_/k>
<models> 
  <model_> 
       <instance_>
       <instName> INSNAME </instName>
        <ofClass> T </ofClass>
      <_/instance>
  <_/model> 
</models> 


syntax K ::= checkOCL ( List )

rule <k> checkOCL(.) => . <_/k>   [structural]

syntax ListItem ::= `OCL ( Exp ) | result ( Val )

rule <k> checkOCL( OCL(E) L:List) => E~>checkOCL(L) <_/k>   [structural]
// when an expression finished evaluating, it is placed at the end of the ocl constraints list

// comment  when "executing" a model
rule <k> v => .K <_/k>
//<metamodel_> <oclConstraints_> (.).List => result(v) </oclConstraints> <_/metamodel>  [structural]

<output_> (.).List => result(v) </output>   [structural]
endkm
