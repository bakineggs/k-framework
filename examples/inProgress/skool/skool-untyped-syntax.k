--- in ../../../k-prelude

kmod SKOOL-UNTYPED-SYNTAX is including PL-INT + PL-ID 

  syntax Var ::= 
                 Id 
               | Exp [ Exp ] [prec 1 strict]
 
  syntax Exp  ::= 
                 Var | Int | Bool
               | ++ Id [prec 0]
               | Exp + Exp [gather(E e) prec 33 strict]
               | Exp - Exp [gather(E e) prec 33 strict]
               | Exp * Exp [gather(E e) prec 31 strict]
               | Exp / Exp [gather(E e) prec 31 strict]
               | Exp % Exp [gather(E e) prec 31 strict]
               | - Exp [strict]
               | Exp < Exp [prec 37 metadata "latex(renameTo _\\ensuremath<_)" strict] 
               | Exp <= Exp [prec 37 metadata "latex(renameTo _\\ensuremath\\leq_)" strict] 
               | Exp > Exp [prec 37 metadata "latex(renameTo _\\ensuremath>_)" strict] 
               | Exp >= Exp [prec 37 metadata "latex(renameTo _\\ensuremath\\geq_)" strict] 
               | Exp == Exp [prec 37 strict] 
               | Exp != Exp [prec 37 strict] 
               | Exp and Exp [gather(E e) prec 55 strict] 
               | Exp or Exp  [gather(E e) prec 59 strict] 
               | not Exp [prec 53 strict]
               | sizeOf ( Exp ) [strict]
               | Exp ( )
               | Exp ( List{Exp} ) [prec 2 strict]
               | read ( )
             --- <OO stuff addded>
               | new Id ( ) [prec 1]
               | new Id ( List{Exp} ) [prec 1 strict(2)]
               | this
               | Exp -> Id ( ) [prec 1 strict(1)]                       --- ???
               | Exp -> Id ( List{Exp} ) [prec 1 strict(1 3)]           --- ???
               | super Id ( ) [prec 1]
               | super Id ( List{Exp} ) [prec 1 strict(2)]
               | Exp instanceOf Id [prec 0 strict(1)]
             --- </OO stuff addded>

  syntax VarDecl ::= 
                 var Var ; [prec 2] 
               | var List{Var} ; [prec 2]

--- <OO stuff addded>
  syntax MethodDecl ::=
                 method Id ( ) Stmt 
               | method Id ( List{Id} ) Stmt 

  syntax ClassDecl ::= 
                 class Id { Stmts }
               | class Id extends Id { Stmts }
--- </OO stuff addded>

  syntax Stmt ::=
                 { } [metadata "latex(renameTo \\{\\})"]
               | { Stmts } [gather(&) metadata "latex(renameTo \\{\_\\})"]
               | Exp = Exp ; [prec 2 strict(2)]
               | Exp ; [prec 50 strict]
               | if Exp then Stmt else Stmt [strict(1)]
               | if Exp then Stmt
               | while Exp do Stmt
               | for Id = Exp to Exp do Stmt
               | return Exp ; [strict]
               | write ( Exp ) ; [strict]

  syntax Stmts ::= 
                 VarDecl 
             --- <OO stuff addded>
               | MethodDecl
               | ClassDecl
             --- </OO stuff addded>
               | Stmt
               | Stmts Stmts [prec 100 gather(e E)]

  syntax Id ::= 
                 main 
             --- <OO stuff addded>
               | Object
             --- </OO stuff addded>

-----------------------------------------------------------------------------
--- Lists should be eventually builtin; for now, they need to be declared ---
-----------------------------------------------------------------------------
--- List{Id}
  syntax List{Id}  ::= 
                 Id 
               | .List{Id}
               | List{Id} , List{Id} [prec 70 assoc id: .List{Id}]

--- List{Var}
  syntax List{Var} ::=
                 Var 
               | List{Id} 
               | List{Var} , List{Var} [ditto]

--- List{Exp}
  syntax List{Exp} ::= 
                 Exp 
               | List{Var} 
               | List{Exp} , List{Exp} [ditto]
-----------------------------------------------------------------------------

endkm


kmod SKOOL-UNTYPED-DESUGARED-SYNTAX is including SKOOL-UNTYPED-SYNTAX 
--------------------------------------------------------------------------------
--- This module desugars some of SIMPLE's language constructs into core ones ---
--- The idea is that we only want to give semantics to core constructs,      ---
--- so we get rid of the derived ones before we start the semantics          ---
--------------------------------------------------------------------------------
  macro if E:Exp then S:Stmt = if E then S else {}
  macro (for X:Id = E1:Exp to E2:Exp do S) = {var X; X=E1; while X <= E2 do {S X=X+1;}}
--- <OO stuff modified>
  macro method F:Id() S = method F(.List{Id}) S
--- </OO stuff modified>
  macro E() = E(.List{Id})

--- <OO stuff addded>
  macro O:Exp -> F() = O -> F(.List{Id})      --- ???
  macro new C:Id() = new C(.List{Id})
  macro O -> F() = O -> F(.List{Id})       --- ???
  macro super F() = super F(.List{Id})

  macro class C {Ss:Stmts} = class C extends Object {Ss}
--- <OO stuff addded>
endkm

